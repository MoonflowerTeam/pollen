plugins {
    id "com.github.johnrengelman.shadow"
}

configurations {
    shadowCommon
}

ext.pollenExtension = parent.extensions.getByName("pollen")

loom {
    splitEnvironmentSourceSets()
}

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath

        compileClasspath += client.compileClasspath
        runtimeClasspath += client.runtimeClasspath
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"

    pollenExtension.moduleDependencies.get().forEach {
        def moduleSource = findProject(":$it:${project.name}").sourceSets
        implementation moduleSource.main.output
        clientImplementation moduleSource.client.output
    }

    testmodImplementation sourceSets.main.output
    testmodImplementation sourceSets.client.output
}

architectury {
    common("fabric", "forge")
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}