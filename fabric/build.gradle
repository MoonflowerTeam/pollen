import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

archivesBaseName = "${rootProject.archivesBaseName}-${project.name}"

architectury {
    platformSetupLoomIde()
    fabric()
}

def generatedResources = file("src/generated/resources")
def accessWidenerFile = project(":common").file("src/main/resources/${rootProject.mod_id}.accesswidener")

sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

loom {
    mixin {
        defaultRefmapName = "${project.mod_id}-refmap.json"
    }
    accessWidenerPath.set(accessWidenerFile)

    runs {
        datagen {
            server()

            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated")}"

            runDir "build/datagen"
        }
    }
}

sourceSets.main.resources { srcDir generatedResources }

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}


repositories {
    maven { url "https://maven.terraformersmc.com/" }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    mavenCentral()
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modApi "com.terraformersmc:modmenu:${rootProject.mod_menu_version}"

    // Common Dependencies
    implementation "com.github.Ocelot5836:molang-compiler:${project.molang_compiler_version}"
    shadowCommon "com.github.Ocelot5836:molang-compiler:${project.molang_compiler_version}"
    implementation "com.electronwill.night-config:core:${project.night_config_version}"
    shadowCommon "com.electronwill.night-config:core:${project.night_config_version}"
    implementation "com.electronwill.night-config:toml:${project.night_config_version}"
    shadowCommon "com.electronwill.night-config:toml:${project.night_config_version}"

    // Fabric-only Dependencies
    modApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cardinal_components_version}"
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cardinal_components_version}"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cardinal_components_version}"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cardinal_components_version}"
    // Needed for making sure render changes apply
    modCompileOnly "curse.maven:sodium-394468:3488820"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

task copyAccessWidener(type: Copy) {
    from(accessWidenerFile)
    into(generatedResources)
}

processResources {
    dependsOn(copyAccessWidener)
    inputs.property "id", rootProject.mod_id
    inputs.property "name", rootProject.mod_name
    inputs.property "version", rootProject.mod_version
    inputs.property "repo", rootProject.repo_name

    filesMatching("fabric.mod.json") {
        expand "id": rootProject.mod_id,
                "name": rootProject.mod_name,
                "version": rootProject.mod_version,
                "repo": rootProject.repo_name
    }

    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
            File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
        }
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    relocate "io.github.ocelot.molangcompiler", "gg.moonflower.pollen.molangcompiler"
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = project.archivesBaseName
            from components.java
        }
    }
}
