plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.10.0-SNAPSHOT" apply false
    id "io.github.juuxel.loom-quiltflower-mini" version "1.0.0" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower-mini"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.mod_id
    version = "${rootProject.mod_version}+${rootProject.minecraft_version}"
    group = rootProject.maven_group

    repositories {
        maven {
            name = "JitPack"
            url = "https://jitpack.io"
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        if (JavaVersion.current().isJava9Compatible()) {
            options.release.set(8)
        }
    }

    java {
        withSourcesJar()
    }

    publishing {
        repositories {
            if (project.hasProperty("jacksonMavenUser") && project.hasProperty("jacksonMavenPass")) {
                maven {
                    url "https://maven.jaackson.me"
                    credentials {
                        username = project.properties.jacksonMavenUser
                        password = project.properties.jacksonMavenPass
                    }
                }
            }
        }
    }
}
